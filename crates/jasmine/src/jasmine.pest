String = @{ "\"" ~ ("\\\"" | "\\r" | "\\n" | "\\t" | (!"\"" ~ ANY))* ~ "\"" }

Enum = @{ "`" ~ (!"`" ~ ANY)* ~ "`" }

WHITESPACE = _{ " " | "\t" | NEWLINE }

blockComment = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") }
lineComment  = _{ "//" ~ (!"\n" ~ ANY)* }
COMMENT      = _{ blockComment | lineComment }

Exp = {
    SqlExp
  | IfExp
  | WhileExp
  | TryExp
  | AssignmentExp
  | UnaryExp
  | BinaryExp
  | Handle
  | Fn
  | FnCall
  | BracketExp
  | Term
}

Statements = { ((Exp | ReturnExp | RaiseExp) ~ ";")* ~ (Exp | ReturnExp | RaiseExp)? }

ControlKeyword = { "if" | "exit" | "while" | "try" | "catch" | "return" | "raise" }

ConditionExp = {
    SqlExp
  | AssignmentExp
  | UnaryExp
  | BinaryExp
  | FnCall
  | Term
}

IfExp = { "if" ~ "(" ~ ConditionExp ~ ")" ~ "{" ~ Statements ~ "}" }

WhileExp = { "while" ~ "(" ~ ConditionExp ~ ")" ~ "{" ~ Statements ~ "}" }

TryExp = { "try" ~ "{" ~ Statements ~ "}" ~ "catch" ~ "(" ~ Id ~ ")" ~ "{" ~ Statements ~ "}" }

BracketExp = { "(" ~ Exp ~ ")" }

// BinaryOp for functional query
ListItem = _{ Exp | BinaryOp }
List     =  { "l[" ~ (ListItem ~ ",")* ~ ListItem? ~ "]" }

ReturnExp = { "return" ~ Exp }
RaiseExp  = { "raise" ~ Exp }

UnaryOperand = _{
    Dataframe
  | Matrix
  | Dict
  | Series
  | List
  | Handle
  | Fn
  | FnCall
  | Id
  | GlobalId
  | BracketExp
}

Handle = { "h" ~ "[" ~ Exp ~ "]" }

UnaryExp = { UnaryOperand ~ Exp }

BinaryOperand = _{
    Fn
  | FnCall
  | Term
  | BracketExp
}

BinaryExp = { BinaryOperand ~ Binary ~ Exp }

Tuple = { "(" ~ ((Id | GlobalId | Skip) ~ ",")* ~ (Id | GlobalId | Skip) ~ ")" }

AssignmentExp = { (FnCall | Id | GlobalId | Tuple) ~ "=" ~ Exp }

Term = _{
    Dataframe
  | Matrix
  | Dict
  | Series
  | List
  | Handle
  | String
  | Enum
  | Timestamp
  | Datetime
  | Duration
  | Date
  | Time
  | Boolean
  | None
  | Decimal
  | Integer
  | Id
  | GlobalId
}

// functions with 2 parameters
BinaryOp = {
    "!=="
  | "!="
  | "<="
  | ">="
  | ">"
  | "<"
  | "===" // strict equality

  | "==" // equality

  | "!"
  | "@"
  | ".." // range

  | "$"
  | "?"
  | "++" // concat list

  | "+"
  | "-"
  | "**"
  | "*"
  | "/"
  | "%"
  | "|"
  | "&"
  | "#"
  | "^"
}

Binary = _{
    BinaryOp
  | BinaryId
}

BinaryId = @{ "~" ~ (Id | GlobalId) }

// <--SQL
SqlKeyword = { "select" | "update" | "delete" | "by" | "from" | "filter" }

SqlOpExp = _{
    BinarySqlExp
  | UnarySqlExp
  | FnCall
  | BracketSqlExp
  | Term
}

UnarySqlExp     =  { (Id | GlobalId) ~ SqlOpExp }
BinarySqlExp    =  { BinaryOperand ~ (BinaryOp | BinaryId | "over") ~ SqlOpExp }
BracketSqlExp   =  { "(" ~ SqlOpExp ~ ")" }
SeriesName      = @{ Id }
SeriesNames     = _{ (SeriesName ~ ",")* ~ SeriesName? }
RenameSeriesExp =  { SeriesName ~ "=" ~ (SqlOpExp | BracketSqlExp) }
SeriesExp       =  { RenameSeriesExp | SqlOpExp | BracketSqlExp }
SeriesExps      = _{ (SeriesExp ~ ",")* ~ SeriesExp? }
FromExp         =  { "from" ~ (Id | GlobalId | BracketExp | FnCall) }
GroupExp        =  { "group" ~ "{" ~ SeriesExps ~ "}" }
FilterExp       =  { "filter" ~ "{" ~ (SqlOpExp ~ ",")* ~ SqlOpExp? ~ "}" }
SelectOp        =  { "select" ~ "{" ~ SeriesExps ~ "}" }
UpdateOp        =  { "update" ~ "{" ~ SeriesExps ~ "}" }
DeleteOp        =  { "delete" ~ "{" ~ SeriesNames ~ "}" }
SortName        = @{ "-"? ~ SeriesName }
SortNames       = _{ SortName ~ ("," ~ SortName)* ~ ","? }
SortOp          =  { "sort" ~ "{" ~ SortNames ~ "}" }
TakeOp          =  { "take" ~ Exp }
SqlExp          =  { FromExp ~ FilterExp? ~ GroupExp? ~ (SelectOp | UpdateOp | DeleteOp)? ~ SortOp? ~ TakeOp? }

// -->SQL

Dataframe = { "df" ~ "[" ~ SeriesExp ~ ("," ~ SeriesExp)* ~ ","? ~ "]" }

Row    = { "[" ~ (Exp ~ ("," ~ Exp)*)? ~ ","? ~ "]" }
Matrix = { "x[" ~ Row* ~ "]" }

KeyValueExp = { Id ~ ":" ~ Exp }
Keys        = { "[" ~ (Exp ~ ",")* ~ Exp? ~ "]" }
Values      = { "[" ~ (Exp ~ ",")* ~ Exp? ~ "]" }
Dict        = {
    ("d" ~ "[" ~ (Keys ~ "," ~ Values)? ~ "]")
  | ("{" ~ (KeyValueExp ~ ",")* ~ KeyValueExp? ~ "}")
}

Params = { "(" ~ (Id ~ ",")* ~ Id? ~ ")" }
Fn     = { "fn" ~ Params ~ "{" ~ ((Exp | ReturnExp | RaiseExp) ~ ";")* ~ (Exp | ReturnExp | RaiseExp)? ~ "}" }

Skip   = { "" }
Arg    = { Exp | Skip }
FnCall = { (GlobalId | Fn | BinaryId | Id | Dict | Dataframe | List | BracketExp | Handle) ~ "(" ~ (Arg ~ ",")* ~ Arg ~ ")" }

Date      = @{ ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} }
Time      = @{ ASCII_DIGIT{2} ~ ":" ~ ASCII_DIGIT{2} ~ ":" ~ ASCII_DIGIT{2} ~ ("." ~ ASCII_DIGIT{,9})? }
Datetime  = @{ Date ~ "T" ~ Time? }
Timestamp = @{ Date ~ "D" ~ Time? }
Duration  = @{ (Integer ~ "D" ~ Time?) | (Integer ~ ("ns" | "s" | "m" | "h")) }
Integer   = @{ "-"? ~ ASCII_DIGIT+ }
Decimal   = @{ "-"? ~ (ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* | "inf") }
Boolean   =  { ("true" | "false") }
None      =  { "none" }
Unknown   =  { (!("," | "]") ~ ANY)* }

Series = { "[" ~ (Unknown ~ ",")* ~ Unknown? ~ "]" }

CJK        =  { HAN | HIRAGANA | KATAKANA | HANGUL | BOPOMOFO }
Identifier = @{ (ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")*) | (CJK ~ (ASCII_ALPHANUMERIC | CJK | "_")*) }
Keyword    =  { SqlKeyword | ControlKeyword }
Id         = @{ (!Keyword ~ Identifier) | (Keyword ~ (ASCII_ALPHANUMERIC | "_")+) }
GlobalId   = @{ Identifier ~ ("." ~ Identifier)* }

Statement = _{ Exp | "" }
Program   = _{ SOI ~ (Statement ~ ";")* ~ Statement? ~ EOI }
